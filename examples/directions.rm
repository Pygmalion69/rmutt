#include "eng.rm"
#include "numbers.rm"
#include "chars.rm"

package directions;

// in-town directions: streets, intersections, signs, places of interest
// in-building directions: floors, stairs, elevators, rooms
// transitions:
// - town to building (arrive at and enter building)
// - building to town (exit building and rejoin street network)

// places of interest:
// - church
// - restaurant
// - library

// special building interiors:
// - hospital
// - restaurant

// streets:
// - "45th" street

import street-number from numbers;

top: town-wander[street,street] "you'll be right back where you started.\n";

_:" ";

// streets
street-name: eng.noun_singular | eng.noun_place | numbers-ordinal.two-digit;
street-type: "street" 3 | "avenue" 3 | "boulevard" | "lane" | "drive";
map-direction: "north" | "south" | "east" | "west";
street: (map-direction _)? street-name (_ street-type)?;

// address
street-address[st]: street-number _ st;

// intersection
int-name: "intersection" | "corner";
intersection[prev,next]:
 "the " int-name " of " prev " and " next;

// town-distance
town-distance: (numbers.nz-digit " blocks") > /one (.+)s/one \1/;

// street-network
town-travel[distance]:
 ("continue"|"go"7) _ (("about"|"approximately")_)? distance;

town-travel-back[distance]:
 ("go back"|"head back"|"go")_
 (("about"|"approximately")_)? distance;

turn-direction: "left" | "right";
reverse-turn[dir]:
  dir > /(.).*/\1/ > /r$/left/ > /l$/right/;

hanga[dir]: ("turn" | "hang a") _ dir;
at: "when you " ("get to"|"arrive at"|"make it to") | "at";

town-turn[prev,next,dir]:
 (at _ (next|intersection[prev,next]) "," _ hanga[dir]) |
 hanga[dir] _ ("onto"|at) _ next;

town-pass[thing]:
 ("continue past"|"you'll pass"|"go past") _ thing _ "on the" _ ("left"|"right");

town-wander[prev,next]:
 (prev-distance = town-distance)
 (dir = turn-direction)
 (landmark = town-landmark[prev]|3)
 town-travel[prev-distance] _ "on" _ prev ".\n"
 landmark > /./ % (town-pass[landmark] ".\n")
 town-turn[prev,next,dir]
 (".\n" town-wander[next,street] 4 |
  (next-distance = town-distance)
  (building = town-building[next])
  " and " town-travel[next-distance] " until you " town-arrive-at[building] ".\n"
  town-enter[building,next] ".\n"
  int ".\n"
  town-exit[building,next] ".\n"
  town-travel-back[next-distance] ".\n")
 town-turn[next,prev,reverse-turn[dir]] ", then "
 town-travel-back[prev-distance,prev]
 landmark > /./ % town-repass[landmark]
 ".\n"; 

town-repass[landmark]:
 (l = landmark > /^a (.*)/the \1/)
 (", passing " l " again" |
 ".\nyou'll see " l " again" |
 " back past " l);

town-landmark:
 "a gas station" |
 "a cell-phone tower" |
 "a " eng.noun_singular " " ("store"|"shop"|"stand") |
 "a drugstore" |
 "a " ("small"|"large") " apartment " ("building"|"complex") |
 town-building-any-street;

town-arrive-at[place]:
 ("reach"|"arrive at"|"get to")_ place;

town-building[street]:
  street-address[street] |
  "the" _ street _ ("church"|"train station"|"YMCA"|"library") |
  town-building-any-street;

town-building-any-street:
  "the " eng.noun_place _ ("building"|"institute"|"center") |
  eng.noun_name "'s" (_ eng.noun_singular _ ("shack"|"palace"|"hut"))? |
  "saint " eng.noun_name "'s" _ ("chapel"|"cathedral"|"church") |
  "the" _ ("police"|"fire") _ "station" |
  ("W"|"K") chars.uc{3};

building-door:
  "the "
  ("front"|"back"|"side"|"street-facing"|"rear-facing"|map-direction) _
  ("door"|"entrance"|"gate");

enter-find:"locate"|"find"|"go to the";

town-enter[building,street]:
 ("enter"|"go into"|"gain entrance to")
 _ building
 (_ ("through"|"via") _ building-door)
| enter-find " the " building-door " that's "
 ("right on"|"down a little bit from"|"set back a bit from") _
 ("the street"|street)
 " and enter the building";

town-exit[building,street]:
 ("exit"|"leave"|"head out of") _ (building|"the building")
 ( " onto " street | "; you'll be back on " street);

// interiors
//

int:
 "once inside, " int-travel ", and go to room " street-number ".\n"
 "on the desk will be a " int-book ". sign your name it.\n"
 ("take the" _ int-lift _ "back to" _ int-entrance |
  ("go back"|"return") _ "to" _ int-entrance _ "on" _ int-lift);

int-entrance:
 "the" _ ("entrance"|"door") (_ "you came in");

int-book:
 "book with the word " eng.noun_singular " written on the cover";

int-lift:
 "the" _ ("service "? "elevator"|"stairs"|"escalator");

int-travel:
 "take" _ int-lift _
 "to the " numbers-ordinal.two-digit " floor" |
 "go" _ ("up"|"down") _ (numbers.nz-digit _ ("floors"|"stories"))>/one (.+)s/one \1/
 ("on the" _ int-lift)?;

