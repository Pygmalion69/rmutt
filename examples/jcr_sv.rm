#include "xml.rm"
#include "eng.rm"
#include "sentence.rm"
#include "url.rm"

package jcr_sv;

top:
  (i="")
  xml.header "\n"
  rootNode;

rootNode:
  node["jcr:root",xml.attr["xmlns:ns",url.url]];

node[n,attrs]:
  (attrs=attrs" "xml.attr["sv:name",n])
  i xml.start["sv:node",attrs] "\n"
  ((i=i"  ")
   properties
   children)
  i xml.end["sv:node"] "\n";

properties:
  (property["String",value[sentence.s]{1,3}] |
   property["Long",value[long]{1,3}]);

property[type,values]:
  i xml.start["sv:property",xml.attr["sv:name",name]" "xml.attr["sv:type",type]] "\n"
    values
  i xml.end["sv:property"] "\n";

value[content]:
  i "  " xml.start["sv:value",] content xml.end["sv:value"] "\n";

long:
  "-"? chars.nzDigit chars.digit{0,5};

children:
  child{0} 16 |
  child{1} 8 |
  child{2} 4 |
  child{3} 2 |
  child{4} 1;

child: node[name,];

name:
  "ns:" eng.noun_singular > /[^A-Za-z]/_/;