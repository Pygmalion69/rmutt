# this is an informal BNF of the syntax for rmutt. consult rmutt.lex
# and rmutt.y for the exact syntax.

grammar ::= (statement | include)*;

include ::= "#include \"" filename "\"";

statement ::= (rule | package) ";"

package ::= "package" package-name;

rule ::= definition | assignment;

definition ::= name ":" body;

assignment ::= name "=" body;

body ::= choice | body choice-separator choice;

choice-separator ::= "," | "|";

choice ::= terms [probability-multiplier] | ;

probability-multiplier ::= integer ;

terms ::= qualified-term | terms qualified-term;

qualified-term ::= term | qualified-term repetition-qualifier | qualified-term ">" term;

repetition-qualifier ::= "{" [min ","] max "}"  | "*" | "+" | ";";

term ::= packaged-label | literal | "(" rule ")" | rx-sub | mapping;

mapping ::= literal "=" literal;

packaged-label ::= label | namespace label;

namespace ::= label ".";

literal ::= "\"" char* "\"";

label ::= label-initial-char label-char*;

rx-sub ::= "/" pattern "/" substitution "/";

label-initial-char ::= [A-Za-z_];

label-char ::= [A-Za-z0-9_-];
